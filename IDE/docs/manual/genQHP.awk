#! /usr/bin/gawk -f

# ==============================================================================
#
# MDS utility for automated generation of Qt Help Project Files from HTML index
# file(s) generated by latex2html converter.
#
# Note:
# this utility always omits the last entry in the HTML index file, this is done
# on purpose since the last entry is expected to be "Index" which is supposed to
# be handled separately.
#
# Usage:
# genQHP.awk -v DNPR=1 -v RESFILE=<file.qhp> <index.html> <file.qhp.in>
#   - DNPR=1 : Do Not PRint final result at the end (it will be written to the
#              file specified in the RESFILE variable instead).
#   - Use @GENQHP_KEYWORDS@ and @GENQHP_SECTIONS@ in the .qhp.in file, these
#     will be automatically replaced with appropriate XML tag sequences by this
#     script.
#
# (C) copyright 2013, 2014 Moravia Microsystems, s.r.o.
#
# author: Martin OÅ¡mera <martin.osmera@moravia-microsystems.com>
#
# ==============================================================================

BEGIN {
    start      = 0
    sectionsNL = 0
    keywordsNL = 0
    indent     = 3
    nodeName   = ""
    nodeUrn    = ""
    indentCh   = ""
    lastClass  = ""

    genIndentation(indent)
}

END {
    if ( ! DNPR ) {
        for ( i = 0; i < sectionsNL; i++ ) {
            print ( sections[i] )
        }
    }
}

function genIndentation ( level ) {
    indentCh = ""
    for ( i = 0; i < level; i++ ) {
        indentCh = indentCh "    "
    }
}

( FILENAME ~ /\.qhp\.in$/ ) {
    if ( RESFILE ) {
        printf("") > RESFILE

        if ( $0 ~ /@GENQHP_SECTIONS@/ ) {
            for ( i = 0; i < sectionsNL; i++ ) {
                print ( sections[i] ) >> RESFILE
            }
        } else if ( $0 ~ /@GENQHP_KEYWORDS@/ ) {
            for ( i = 0; i < keywordsNL; i++ ) {
                print ( keywords[i] ) >> RESFILE
            }
        } else {
            print($0) >> RESFILE
        }
    }

    next
}

/div class="tableofcontents"/ {
    start = 1

#     sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"%s\">", indentCh, NAME, INDEX_FILE )

    lastClass = "C"
#     indent++
    genIndentation(indent)
}

/href=".+\.html(#[^"]*)?"/ && ( start ) {
    if ( ( "" != nodeName ) && ( "" != nodeUrn ) ) {
        sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"%s\"/>", indentCh, nodeName, nodeUrn )
    }

    match ( $0, />[^<>]*</ )
    nodeName = substr ( $0, RSTART + 1, RLENGTH - 2 )

    if ( "Contents" == nodeName )
    {
        nodeName = ""
        next
    }

    match ( $0, /"[^\"]*"/ )
    nodeUrn = substr ( $0, RSTART + 1, RLENGTH - 2 )
}

/class="chapterToc"/ && ( start ) {
    if ( "C" != lastClass ) {
        if ( ( "" != nodeName ) && ( "" != nodeUrn ) ) {
            sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"%s\"/>", indentCh, nodeName, nodeUrn )
            nodeName = ""
            nodeUrn  = ""
        }

        while ( "C" != lastClass )
        {
            indent--
            genIndentation(indent)
            sections [ sectionsNL++ ] = sprintf ( "%s</section>", indentCh )

            if      ( "S"   == lastClass ) { lastClass = "C"  }
            else if ( "SS"  == lastClass ) { lastClass = "S"  }
            else if ( "SSS" == lastClass ) { lastClass = "SS" }
        }
    }
}

/class="sectionToc"/ && ( start ) {
    if ( "C" == lastClass ) {
        sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"%s\">", indentCh, nodeName, nodeUrn )

        nodeName = ""
        nodeUrn  = ""

        indent++
        genIndentation(indent)
    } else if ( ( "SS" == lastClass ) || ( "SSS" == lastClass ) ) {
        sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"%s\"/>", indentCh, nodeName, nodeUrn )
        nodeName = ""
        nodeUrn  = ""

        indent--
        genIndentation(indent)
        sections [ sectionsNL++ ] = sprintf ( "%s</section>", indentCh )

        if ( "SSS" == lastClass ) {
            indent--
            genIndentation(indent)
            sections [ sectionsNL++ ] = sprintf ( "%s</section>", indentCh )
        }
    }

    lastClass = "S"
}

/class="subsectionToc"/ && ( start ) {
    if ( "S" == lastClass ) {
        sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"%s\">", indentCh, nodeName, nodeUrn )

        nodeName = ""
        nodeUrn  = ""

        indent++
        genIndentation(indent)
    } else if ( "SSS" == lastClass ) {
        sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"%s\"/>", indentCh, nodeName, nodeUrn )
        nodeName = ""
        nodeUrn  = ""

        indent--
        genIndentation(indent)
        sections [ sectionsNL++ ] = sprintf ( "%s</section>", indentCh )
    }

    lastClass = "SS"
}

/class="subsubsectionToc"/ && ( start ) {
    if ( "SS" == lastClass ) {
        sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"%s\">", indentCh, nodeName, nodeUrn )

        nodeName = ""
        nodeUrn  = ""

        indent++
        genIndentation(indent)
    }

    lastClass = "SSS"
}

/class="likechapterToc"/ && ( start ) {
    if ( "Index" == nodeName ) {
        sub ( /#.+$/, "", nodeUrn )

        indexHtmlFile = nodeUrn
        indexStart = 0
        indexName = ""
        indexUrl = ""

        while ( getline line < indexHtmlFile ) {
            if ( indexStart ) {
                if ( line ~ /href=".+\.html(#[^"]*)?"/ ) {
                    match ( line, /"[^\"]*"/ )
                    indexUrl = substr ( line, RSTART + 1, RLENGTH - 2 )
                }
                if ( line ~ /class="index-item"/ ) {
                    match ( line, />[^<>]*</ )
                    indexName = substr ( line, RSTART + 1, RLENGTH - 2 )
                    sub ( /[, ]+$/, "", indexName )
                }

                if ( ( "" != indexName ) && ( "" != indexUrl ) ) {
                    keywords [ keywordsNL++ ] = sprintf ( "%s<keyword name=\"%s\" ref=\"%s\"/>", \
                                                          "            ", \
                                                          indexName, \
                                                          indexUrl )
                    indexName = ""
                    indexUrl = ""
                }
            }

            if ( line ~ /class="theindex"/ ) {
                indexStart = 1
            } else if ( line ~ /<\/div>/ ) {
                indexStart = 0
            }
        }
    }
}

/<\/div>/ && ( start ) {
    start = 0

    if ( ( "" != nodeName ) && ( "" != nodeUrn ) ) {
        sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"%s\"/>", indentCh, nodeName, nodeUrn )
    }

    if ( "SSS" == lastClass ) {
        lastClass = "SS";
        indent--
        genIndentation(indent)
        sections [ sectionsNL++ ] = sprintf ( "%s</section>", indentCh )
    }

    if ( "SS" == lastClass ) {
        lastClass = "S";
        indent--
        genIndentation(indent)
        sections [ sectionsNL++ ] = sprintf ( "%s</section>", indentCh )
    }

    if ( "S" == lastClass ) {
        lastClass = "C";
        indent--
        genIndentation(indent)
        sections [ sectionsNL++ ] = sprintf ( "%s</section>", indentCh )
    }

#     if ( "C" == lastClass ) {
#         indent--
#         genIndentation(indent)
#         sections [ sectionsNL++ ] = sprintf ( "%s</section>", indentCh )
#     }
}
