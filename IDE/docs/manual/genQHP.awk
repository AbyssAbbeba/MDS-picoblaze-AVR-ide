#! /usr/bin/gawk -f

# ==============================================================================
#
# MDS utility for automated generation of Qt Help Project Files from HTML index
# file(s) generated by latex2html converter.
#
# Note:
# this utility always omits the last entry in the HTML index file, this is done
# on purpose since the last entry is expected to be "Index" which is supposed to
# be handled separately.
#
# (C) copyright 2013 Moravia Microsystems, s.r.o.
#
# author: Martin OÅ¡mera <martin.osmera@moravia-microsystems.com>
#
# ==============================================================================

BEGIN {
    start      = 0
    sectionsNL = 0
    keywordsNL = 0
    indent     = 3
    nodeName   = ""
    nodeUrn    = ""
    indentCh   = ""

    genIndentation(indent)
}

END {
    if ( ! DNPR )
    {
        for ( i = 0; i < sectionsNL; i++ )
        {
            print ( sections[i] )
        }
    }
}

function genIndentation ( level ) {
    indentCh = ""
    for ( i = 0; i < level; i++ )
    {
        indentCh = indentCh "    "
    }
}

function processIndexFile ( indexFile ) {
    indent = 3
    genIndentation(indent)

    while ( 1 == getline line < indexFile )
    {
        if ( line ~ /<DT>/ )
        {
            match ( line, />[^<>\/]*<\// )
            name = substr ( line, RSTART + 1, RLENGTH - 3 )
        }
        else if ( line ~ /<DD>/ )
        {
            match ( line, /"[^\"]*"/ )
            urn = substr ( line, RSTART + 1, RLENGTH - 2 )

            keywords [ keywordsNL++ ] = sprintf ( "%s<keyword name=\"%s\" ref=\"MDS_manual/%s\"/>", \
                                                  indentCh, name, urn )
        }
    }
}

( FILENAME ~ /\.qhp\.in$/ ) {
    if ( RESFILE )
    {
        printf("") > RESFILE

        if ( $0 ~ /\{@GENQHP_SECTIONS@\}/ )
        {
            for ( i = 0; i < sectionsNL; i++ )
            {
                print ( sections[i] ) >> RESFILE
            }
        }
        else if ( $0 ~ /\{@GENQHP_KEYWORDS@\}/ )
        {
            for ( i = 0; i < keywordsNL; i++ )
            {
                print ( keywords[i] ) >> RESFILE
            }
        }
        else
        {
            print($0) >> RESFILE
        }
    }

    next
}

/NAME="CHILD_LINKS"/ {
    start = 1

    sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"MDS_manual/%s\">", \
                                          indentCh, "MDS Manual", "index.html" )

    indent++
    genIndentation(indent)
}

/HREF="node[0-9]+\.html"/ && ( start ) {
    if ( ( "" != nodeName ) && ( "" != nodeUrn ) )
    {
        sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"MDS_manual/%s\"/>", \
                                              indentCh, nodeName, nodeUrn )
    }

    match ( $0, />[^<>]*</ )
    nodeName = substr ( $0, RSTART + 1, RLENGTH - 2 )

    match ( $0, /"[^\"]*"/ )
    nodeUrn = substr ( $0, RSTART + 1, RLENGTH - 2 )

    if ( "Index" == nodeName )
    {
        processIndexFile( "MDS_manual/" nodeUrn )
    }
}

/<UL>/ && ( start ) {
    sections [ sectionsNL++ ] = sprintf ( "%s<section title=\"%s\" ref=\"MDS_manual/%s\">", \
                                          indentCh, nodeName, nodeUrn )

    nodeName = ""
    nodeUrn  = ""

    indent++
    genIndentation(indent)
}

/<\/UL>/ && ( start ) {
    indent--
    genIndentation(indent)

    sections [ sectionsNL++ ] = sprintf ( "%s</section>", indentCh )
}
