;==============================================================================;
;
; Tesla Agregator
; Michal Kubicek (michal.kubicek@email.cz)
;
;==============================================================================;
;
;   I  N     N  I  TTTTTTTTT  I    AAA    L        I  ZZZZZZZ    AAA    TTTTTTTTT  I   OOOOOO   N     N
;   I  NN    N  I      T      I   A   A   L        I       Z    A   A       T      I  O      O  NN    N
;   I  N N   N  I      T      I  A     A  L        I      Z    A     A      T      I  O      O  N N   N
;   I  N  N  N  I      T      I  AAAAAAA  L        I    ZZZ    AAAAAAA      T      I  O      O  N  N  N
;   I  N   N N  I      T      I  A     A  L        I    Z      A     A      T      I  O      O  N   N N
;   I  N    NN  I      T      I  A     A  L        I   Z       A     A      T      I  O      O  N    NN
;   I  N     N  I      T      I  A     A  LLLLLLL  I  ZZZZZZZ  A     A      T      I   0OOOO0   N     N
;
;==============================================================================;

SYS_init:           CALL        wait_100ms

;==============================================================================;
; Agregator setup
;==============================================================================;

                    ; System reset
                    LOAD        s0, 01
                    OUTPUT      s0, ID_SYS_reset    ; self-clearing
                    CALL        wait_100ms

                    ; MAC to Agregator interconnection setting register
                    ; 7:6 disconnected, 5:4 LAG, 3:2 RAD, 1:0 PRI
                    ;                - L R P
                    ;                0 2 3 1
                    LOAD        s0, 00101101'b
                    OUTPUT      s0, ID_MAC_port_MUX_control

                    ; set AGR to Isolate mode
                    LOAD        s0, Mode_Isolate
                    OUTPUT      s0, ID_AGR_mode_req

                    ; Set limit to 4096 Bytes (0x1000); [C_SwLim_4k0_H & C_SwLim_xkx_L]
                    LOAD        s0, C_SwLim_4k0_H
                    OUTPUT      s0, ID_AGR_Switch_Limit_RAD_H
                    LOAD        s0, C_SwLim_xkx_L
                    OUTPUT      s0, ID_AGR_Switch_Limit_RAD_L
                    LOAD        s0, C_SwLim_4k0_H
                    OUTPUT      s0, ID_AGR_Switch_Limit_LAG_H
                    LOAD        s0, C_SwLim_xkx_L
                    OUTPUT      s0, ID_AGR_Switch_Limit_LAG_L

;==============================================================================;
; onboard LED blink at startup
;==============================================================================;
                    LOAD        sC, FF
                    OUTPUT      sC, ID_GPIO_LED
                    CALL        wait_100ms
                    LOAD        sC, 00
                    OUTPUT      sC, ID_GPIO_LED
                    CALL        wait_100ms

;==============================================================================;
; UART setup
;==============================================================================;
                    CALL        reset_UART      ; no internal delay
                    CALL        wait_10ms

;------------------------------------------------------------------------------;
; Message: Wellcome message
                    CALL        wellcome_msg

;------------------------------------------------------------------------------;
; Message: Peripheral startup initialization
                    CALL        mess_InitPer
                    CALL        send_CRLF



;==============================================================================;
; I2C "EEPROM" setup
;==============================================================================;
                    LOAD        s0, 53                  ; set I2C slave address to 0x53
                    OUTPUT      s0, ID_I2C_Dev_addr
;------------------------------------------------------------------------------;
; write test string to EEPROM (from address 0x80 up); 31 characters
;       "SW build:  14:17:58 31 Jul 2012"

; load starting address of write accessible sector in EEPROM
                    LOAD        s4, 80
; "SW build:  "; 11
I2C_WR_st_SW:       LOAD        sB, st_SW'upper
                    LOAD        sA, st_SW'lower
                    CALL        I2C_WR_string
; "14:17:58"; 8
I2C_WR_st_time:     LOAD        sB, st_TimeStamp'upper
                    LOAD        sA, st_TimeStamp'lower
                    CALL        I2C_WR_string
; store 1 space character; 1
                    LOAD        s5, 20                  ; space character
                    CALL        EEPROM_WR               ; write data to I2C memory
                    ADD         s4, 01                  ; increment I2C address
; "31 Jul 2012"; 11
I2C_WR_st_date:     LOAD        sB, st_DateStamp'upper
                    LOAD        sA, st_DateStamp'lower
                    CALL        I2C_WR_string

; clear byte 0x9F (status register)
                    LOAD        s4, 9F                      ; set accessed address
                    LOAD        s5, 00
                    CALL        EEPROM_WR

;==============================================================================;
; Interrupt controler
;==============================================================================;
;  1 = UART_Status_reg(3);          -- level sensitive; UART RX_data_present (new data)
;  2 = PHY_MDINT(0);                -- level sensitive
;  3 = PHY_MDINT(1);                -- level sensitive
;  4 = PHY_MDINT(2);                -- level sensitive
;  5 = PHY_MDINT(3);                -- level sensitive
;  6 = SFP_present;                 -- edge sensitive
;  7 = FIFO overflow/underflow      -- level sensitive
;  8 = -- unused --
                    LOAD        s0, 01011111'b
                    OUTPUT      s0, ID_INT_EN
                    LOAD        s0, 00100000'b          ; only SFP IRQ is edge sensitive
                    OUTPUT      s0, ID_INT_SENSE



;==============================================================================;
; SFP transceiver setup
;==============================================================================;
                    CALL        mess_SFP
                    CALL        send_CRLF

        ; enable Tx and select high data rate (RX = '1' )
        ;  SFP_TXDIS     <= ID_SFP_control(6);
        ;  SFP_RS        <= ID_SFP_control(7);
        ;  --  SFP_TXDIS     <= '0';
        ;  --  SFP_RS        <= '1';

                    LOAD        sF, 80
                    OUTPUT      SF, ID_SFP_control

       ; SFP PHY setup (88E1111 only) - set to SGMII to Copper mode (GBIC mode is currently not supported)
                    LOAD        sF, 00                  ; clear status register
                    CALL        Setup_SFP



;==============================================================================;
; VITESSE PHY setup
;==============================================================================;
                    CALL        mess_PHY
                    CALL        send_CRLF
                    CALL        PHY_init
                    CALL        send_CRLF



;==============================================================================;
; Update link speed info (all ports)
;==============================================================================;
                    CALL        wait_100ms
                    CALL        report_all

;==============================================================================;
; Performe test write to EEPROM
;==============================================================================;

; write intitial value to EEPROM
                    LOAD        s3, 53                  ; I2C device address
                    LOAD        s4, 00                  ; CMD register address
                    LOAD        s5, 11101100'b          ; (0) = '0'         RF_CONNECT_A failure ('0' = radio is online)
                                                        ; (4:1) = "0110"    RADIO_STAT 256 QAM 4k
                                                        ; (7:5) = "000"     MODE Isolate
                                                        ;         "001"     MODE Loopback
                                                        ;         "010"     MODE Direct
                                                        ;         "011"     MODE AGR_slave
                                                        ;         "100"     MODE AGR_master
                                                        ;         "101"     -- reserved --
                                                        ;         "110"     -- reserved --
                                                        ;         "111"     MODE Auto
                    CALL        I2C_radio_write

;==============================================================================;
; HOST interface setup
;==============================================================================;

; Set MDIO_0 clock speed via HOST_0 interface (write 0x0000007F to address 0x340): Fclk MDIO ~ 1 MHz

                    LOAD        sD, 00                      ; select first HOST interface

host_setup_i:       OUTPUT      sD, ID_HOST_select          ; set access to a particular HOST interface
                    LOAD        s3, 40                      ; ID_HOST_addr_L
                    LOAD        s4, 03                      ; ID_HOST_addr_H
                    LOAD        s5, 7F                      ; ID_HOST_data_0
                    LOAD        s6, 00                      ; ID_HOST_data_1
                    LOAD        s7, 00                      ; ID_HOST_data_2
                    LOAD        s8, 00                      ; ID_HOST_data_3
                    CALL        wr_HOST_MAC
                    ADD         sD, 01                      ; select next HOST interface
                    COMPARE     sD, 04                      ; last HOST set?
                    JUMP        NZ, host_setup_i


;==============================================================================;
; PCS/PMA setup
;==============================================================================;

                    LOAD        sD, 00                      ; select first HOST interface
PCS_PMA_setup:      OUTPUT      sD, ID_HOST_select          ; set access to a particular HOST interface

                    ; register 0: bit_10 (isolate) <= 0
                    LOAD        s3, 01                      ; device address; only bits 4:0 are valid
                    LOAD        s4, 00                      ; register address byte; only bits 4:0 are valid
                    LOAD        s5, 40                      ; data LSB
                    LOAD        s6, 11                      ; data MSB
                    CALL        wr_HOST_MDIO

                    ADD         sD, 01                      ; select next HOST interface
                    COMPARE     sD, 04                      ; last HOST set?
                    JUMP        NZ, PCS_PMA_setup


;==============================================================================;
; Final setup
;==============================================================================;

                    CALL        send_CRLF
                    CALL        mess_Done
                    CALL        send_CRLF
                    CALL        send_CRLF
                    RETURN

;==============================================================================;
; End of initialization
;==============================================================================;




;==============================================================================;
; Soft restart for FIFO overflow issues
;==============================================================================;

Soft_restart:       ; Agregator reset (system reset)
                    LOAD        s0, 01
                    OUTPUT      s0, ID_SYS_reset    ; self-clearing

                    ; MAC to Agregator interconnection setting register
                    ; 7:6 disconnected, 5:4 LAG, 3:2 RAD, 1:0 PRI
                    ;                - L R P
                    ;                0 2 3 1
                    LOAD        s0, C_MAC_0x_2L_3R_1P
                    OUTPUT      s0, ID_MAC_port_MUX_control

                    ; set AGR to Isolate mode
                    LOAD        s0, Mode_Isolate
                    OUTPUT      s0, ID_AGR_mode_req

                    ; Set limit to 4096 Bytes (0x1000); [C_SwLim_4k0_H & C_SwLim_xkx_L]
                    LOAD        s0, C_SwLim_4k0_H
                    OUTPUT      s0, ID_AGR_Switch_Limit_RAD_H
                    LOAD        s0, C_SwLim_xkx_L
                    OUTPUT      s0, ID_AGR_Switch_Limit_RAD_L
                    LOAD        s0, C_SwLim_4k0_H
                    OUTPUT      s0, ID_AGR_Switch_Limit_LAG_H
                    LOAD        s0, C_SwLim_xkx_L
                    OUTPUT      s0, ID_AGR_Switch_Limit_LAG_L

;------------------------------------------------------------------------------;
; UART reset
                    CALL        reset_UART      ; no internal delay
                    CALL        wait_10ms

;------------------------------------------------------------------------------;

                    LOAD        s0, 01011111'b
                    OUTPUT      s0, ID_INT_EN
                    LOAD        s0, 00100000'b          ; only SFP IRQ is edge sensitive
                    OUTPUT      s0, ID_INT_SENSE

                    ENABLE INTERRUPT            ; By default interrupt is disabled after startup !!!
                    RETURN
;==============================================================================;
